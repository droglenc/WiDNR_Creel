/*  STANDARD PROGRAM  - ANY NUMBER OF POSSIBLE SHIFTS*/
/*  MODIFIED TO AUTOMATICALLY DESIGNATE ALL HOLIDAYS*/
/*  MODIFIED TO PRODUCE CONTINUOUS RANDOM NUMBER STREAM */
/*  AUTOMATICALLY GENERATES NON-OVERLAPPING SHIFT TIMES*/
/*  OVERLAPPING SHIFTS NOT ALLOWED (NOT NEEDED)*/
/*  CLERKS MAY WORK TIMES OTHER THAN THOSE GENERATED*/
/*  BUT COLLECT ONLY INTERVIEWS DURING THOSE TIME*/
/*  PROGRAM LAST MODIFIED 9/20/90 - LATEST VERSION FOR INLAND*/

%MACRO RAND;
  TEMPSEED=1;
  TEMPSEED=SYMGET('SEED');
  CALL RANUNI(TEMPSEED,RANDOM);
  CALL SYMPUT('SEED',TEMPSEED);
  IF RANDOM GT 1.0 THEN DO;
    PUT 'SEED TOO LARGE';
    ABORT;
  END;
%MEND RAND;

%MACRO SHIFTS;
OPTIONS NODATE NONUMBER;
DATA SHIFTS;
INPUT DATE1 MMDDYY8. +1 DATE2 MMDDYY8. +1 START TIME5. +1 END TIME5.
SHIFTLEN DAYS;
DO DATE=DATE1 TO DATE2;
  KEEP DATE START END SHIFTLEN DAYS;
  OUTPUT;
END;
%MEND SHIFTS;

%MACRO INTCOUNT;
DATA CALENDAR;
WEEK=1;
DO DATE=&SDATE TO &FDATE;           /*GENERATE SAMPLING DATES*/
  MONTH=MONTH(DATE);
  DAY=DAY(DATE);
  WDAY=WEEKDAY(DATE);
  IF WDAY=1 THEN WEEK=WEEK+1;
  IF WDAY=1 OR WDAY=7 THEN DAYTYPE=1; ELSE DAYTYPE=2;
  IF MONTH=1 AND DAY=1 THEN DAYTYPE=1;                   *NEW YEARS DAY;
  IF MONTH=5 AND 25<=DAY<=31 AND WDAY=2 THEN DAYTYPE=1;        *MEM DAY;
  IF MONTH=7 AND DAY=4 THEN DAYTYPE=1;                        *JULY 4TH;
  IF MONTH=9 AND 1<=DAY<=7 AND WDAY=2 THEN DAYTYPE=1;        *LABOR DAY;
  IF DAYTYPE=1 THEN SELECT=1; ELSE SELECT=0;
  %RAND;
  KEEP DATE DAYTYPE WEEK SELECT RANDOM;
  OUTPUT;
END;

DATA CALENDAR;   /*ADD SHIFT TIMES TO DATES*/
MERGE CALENDAR(IN=A) SHIFTS(IN=B);
BY DATE;
IF A AND B;

PROC SUMMARY NWAY;  /*COUNT NUMBER OF WEEKEND AND HOLIDAYS PER WEEK*/
CLASSES WEEK;
VAR SELECT;
OUTPUT OUT=DAYS SUM=NUMBER;
DATA CALENDAR;
MERGE CALENDAR DAYS;
BY WEEK;
KEEP DATE DAYTYPE WEEK SELECT RANDOM NUMBER START END SHIFTLEN DAYS;
PROC SORT;
BY WEEK RANDOM;

DATA CALENDAR;    /*SELECT WEEKDAYS TO FILL OUT DESIRED WORKWEEK*/
SET CALENDAR;
BY WEEK;
RETAIN COUNT;
IF FIRST.WEEK THEN COUNT=NUMBER;
IF SELECT=0 AND COUNT LT DAYS THEN DO;
  COUNT=COUNT+1;
  SELECT=1;
END;
KEEP DATE DAYTYPE WEEK SELECT START END SHIFTLEN;
IF SELECT=1;
PROC SORT;
BY DAYTYPE DATE;

PROC SUMMARY NWAY;  /*COUNTS NUMBER OF SAMPLED DAYS PER DAYTYPE*/
CLASSES DAYTYPE;
VAR DATE;
OUTPUT OUT=UNITS N=NUMBER;

DATA UNITS;  /*GENERATES SETS OF SAMPLING UNITS WOUT REPLACEMENT*/
SET UNITS;
NUMBER=CEIL(NUMBER/&UNITS);
SET=1;
DO I=1 TO NUMBER;
  DO UNIT=1 TO &UNITS;
    %RAND;
    KEEP DAYTYPE SET UNIT RANDOM;
    OUTPUT;
  END;
  SET=SET+1;
END;
PROC SORT;
BY DAYTYPE SET RANDOM;

DATA CALENDAR;  /*ADDS RANDOMLY ORDERED SAMPLING UNITS TO DATES*/
MERGE CALENDAR(IN=A) UNITS(IN=B);
BY DAYTYPE;
IF A AND B;
KEEP DATE DAYTYPE WEEK UNIT START END SHIFTLEN;
PROC SORT;
BY DAYTYPE UNIT DATE;

PROC SUMMARY NWAY;  /*COUNTS NUMBER SAMPLED DAYS PER DAYTYPE-UNIT*/
CLASSES DAYTYPE UNIT;
VAR DATE;
OUTPUT OUT=SHIFTX N=NUMBER;

DATA SHIFTX;  /*GENERATES SETS OF SAMPLING SHIFTS WOUT REPLACEMENT*/
SET SHIFTX;
NUMBER=CEIL(NUMBER/&SHIFTS);
SET=1;
DO I=1 TO NUMBER;
  DO SHIFT=1 TO &SHIFTS;
    %RAND;
    KEEP DAYTYPE SET UNIT SHIFT RANDOM;
    OUTPUT;
  END;
  SET=SET+1;
END;
PROC SORT;
BY DAYTYPE UNIT SET RANDOM;

DATA CALENDAR;  /*ADDS RANDOMLY ORDERED SHIFTS TO DATES*/
MERGE CALENDAR(IN=A) SHIFTX(IN=B);
BY DAYTYPE UNIT;
IF A AND B;
KEEP DATE DAYTYPE WEEK UNIT SHIFT START END SHIFTLEN;

DATA CALENDAR;  /*GENERATE INSTANTANEOUS COUNT TIMES*/
SET CALENDAR;
INTV1=(END-START)/&SHIFTS;    /*NON-OVERLAPPING SHIFT LENGTH*/
INTV2=(INTV1)/&COUNTS;        /*TIME BETWEEN COUNTS*/
%RAND;
TIME1=INT(RANDOM*INTV2);         /*RELATIVE TIME OF 1ST COUNT*/
TIME1=TIME1+(START+(SHIFT-1)*INTV1);  /*ACTUAL TIME OF 1ST COUNT*/
%DO X=2 %TO &COUNTS;          /*GENERATE REST OF COUNTS*/
  %LET Y=%EVAL(&X-1);
  TIME&X=TIME&Y + INTV2;
%END;
KEEP DATE DAYTYPE UNIT SHIFT TIME1-TIME&COUNTS START END SHIFTLEN;
PROC SORT;
BY DAYTYPE UNIT SHIFT DATE;

PROC SUMMARY NWAY;     /*SELECT ORDER OF SITES TO BE SAMPLED*/
CLASSES DAYTYPE UNIT SHIFT;
VAR DATE;
OUTPUT OUT=NEW N=NUMBER;

DATA NEW; /*GENERATE RANDOM ORDER OF SITES TO BE SAMPLED*/
SET NEW;
%DO X=1 %TO &UNITS;
  IF UNIT=&X THEN SNUM=SYMGET("SITES&X");
%END;
NUMBER=CEIL(NUMBER/SNUM);
SET=1;
DO I=1 TO NUMBER;
  DO SITE=1 TO SNUM;
    %RAND
    KEEP DAYTYPE UNIT SHIFT SITE SET RANDOM;
    OUTPUT;
  END;
  SET=SET+1;
END;
PROC SORT;
BY DAYTYPE UNIT SHIFT SET RANDOM;

DATA CALENDAR;
MERGE CALENDAR(IN=A) NEW(IN=B);
BY DAYTYPE UNIT SHIFT;
IF A AND B;
KEEP DATE DAYX SITEX SHIFTX TIME1-TIME&COUNTS;
LENGTH TEMP1 TEMP2 SITEX $ 5 SHIFTX DAYX $ 11;
TEMP1=PUT(START,HHMM5.);
TEMP2=PUT(END,HHMM5.);
DAYX=COMPRESS(TEMP1||'-'||TEMP2);
INTV1=(END-START)/&SHIFTS;    /*NON-OVERLAPPING SHIFT LENGTH*/
START=START+(SHIFT-1)*INTV1;  /*BEGINNING TIME OF SHIFT*/
END=START+INTV1;              /*ENDING TIME OF SHIFT*/
TEMP1=PUT(START,HHMM5.);
TEMP2=PUT(END,HHMM5.);
SHIFTX=COMPRESS(TEMP1||'-'||TEMP2);
SITEX=COMPRESS(UNIT||SITE);
LABEL TIME1='COUNT TIME'
      TIME2='COUNT TIME'
      TIME3='COUNT TIME'
      TIME4='COUNT TIME'
      TIME5='COUNT TIME'
      TIME6='COUNT TIME'
      SITEX='SAMPLE LOCATION'
      DAYX='ASSUMED DAY LENGTH'
      SHIFTX='WORK SHIFT';
PROC SORT;
BY DATE;

PROC CALENDAR HEADER=SMALL;
FORMAT TIME1-TIME&COUNTS HHMM5. SITEX $SITE.;
ID DATE;
VAR SITEX SHIFTX TIME1-TIME&COUNTS;
%MEND INTCOUNT;
