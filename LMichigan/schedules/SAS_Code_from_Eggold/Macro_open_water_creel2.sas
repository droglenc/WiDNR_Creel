/*  STANDARD PROGRAM  - 2 POSSIBLE SHIFTS*/
%MACRO RAND;
  RANDOM=RANUNI(&SEED);
  IF RANDOM GT 1.0 THEN DO;
    PUT 'SEED TOO LARGE';
    ABORT;
  END;
%MEND RAND;

%MACRO SHIFTS;
OPTIONS NODATE NONUMBER;
DATA SHIFTS;
INPUT DATE1 MMDDYY8. +1 DATE2 MMDDYY8. +1 START TIME5. +1 END TIME5.
SHIFTLEN DAYS;
DO DATE=DATE1 TO DATE2;
  STARTAM=START;
  ENDAM=START+(SHIFTLEN*3600);
  STARTPM=END-(SHIFTLEN*3600);
  ENDPM=END;
  KEEP DATE STARTAM ENDAM STARTPM ENDPM DAYS;
  OUTPUT;
END;
%MEND SHIFTS;

%MACRO INTCOUNT;
DATA CALENDAR;
DO I=1 TO 100;
  %RAND
END;
WEEK=1;
DO DATE=&SDATE TO &FDATE;           /*GENERATE SAMPLING DATES*/
  MONTH=MONTH(DATE);
  DAY=DAY(DATE);
  WDAY=WEEKDAY(DATE);
  IF WDAY=1 THEN WEEK=WEEK+1;
  IF WDAY=1 OR WDAY=7 THEN DAYTYPE=1; ELSE DAYTYPE=2;
  IF MONTH=1 AND DAY=1 THEN DAYTYPE=1;              /*NEW YEARS DAY*/
  IF MONTH=5 AND 25<=DAY<=31 AND WDAY=2 THEN DAYTYPE=1;   /*MEM DAY*/
  IF MONTH=7 AND DAY=4 THEN DAYTYPE=1;                   /*JULY 4TH*/
  IF MONTH=9 AND 1<=DAY<=7 AND WDAY=2 THEN DAYTYPE=1;   /*LABOR DAY*/
  IF MONTH=11 AND 22<=DAY<=28 AND WDAY=5 THEN DAYTYPE=1;    /*T-DAY*/
  IF MONTH=12 AND DAY=25 THEN DAYTYPE=1;                 /*XMAS DAY*/
  IF DAYTYPE=1 THEN SELECT=1; ELSE SELECT=0;
  %RAND;
  KEEP DATE DAYTYPE WEEK SELECT RANDOM;
  OUTPUT;
END;

DATA CALENDAR;   /*ADD SHIFT TIMES TO DATES*/
MERGE CALENDAR(IN=A) SHIFTS(IN=B);
BY DATE;
IF A AND B;

PROC SUMMARY NWAY;  /*COUNT NUMBER OF WEEKEND AND HOLIDAYS PER WEEK*/
CLASSES WEEK;
VAR SELECT;
OUTPUT OUT=DAYS SUM=NUMBER;
DATA CALENDAR;
MERGE CALENDAR DAYS;
BY WEEK;
KEEP DATE DAYTYPE WEEK SELECT RANDOM NUMBER STARTAM ENDAM STARTPM ENDPM
DAYS;
PROC SORT;
BY WEEK RANDOM;

DATA CALENDAR;    /*SELECT WEEKDAYS TO FILL OUT DESIRED WORKWEEK*/
SET CALENDAR;
BY WEEK;
RETAIN COUNT;
IF FIRST.WEEK THEN COUNT=NUMBER;
IF SELECT=0 AND COUNT LT DAYS THEN DO;
  COUNT=COUNT+1;
  SELECT=1;
END;
KEEP DATE DAYTYPE WEEK SELECT STARTAM ENDAM STARTPM ENDPM;
IF SELECT=1;
PROC SORT;
BY DAYTYPE DATE;

PROC SUMMARY NWAY;  /*COUNTS NUMBER OF SAMPLED DAYS PER DAYTYPE*/
CLASSES DAYTYPE;
VAR DATE;
OUTPUT OUT=UNITS N=NUMBER;

DATA UNITS;  /*GENERATES SETS OF SAMPLING UNITS WOUT REPLACEMENT*/
SET UNITS;
NUMBER=CEIL(NUMBER/&UNITS);
SET=1;
DO I=1 TO NUMBER;
  DO UNIT=1 TO &UNITS;
    %RAND;
    KEEP DAYTYPE SET UNIT RANDOM;
    OUTPUT;
  END;
  SET=SET+1;
END;
PROC SORT;
BY DAYTYPE SET RANDOM;

DATA CALENDAR;  /*ADDS RANDOMLY ORDERED SAMPLING UNITS TO DATES*/
MERGE CALENDAR(IN=A) UNITS(IN=B);
BY DAYTYPE;
IF A AND B;
KEEP DATE DAYTYPE WEEK UNIT STARTAM ENDAM STARTPM ENDPM;
PROC SORT;
BY DAYTYPE UNIT DATE;

PROC SUMMARY NWAY;  /*COUNTS NUMBER SAMPLED DAYS PER DAYTYPE-UNIT*/
CLASSES DAYTYPE UNIT;
VAR DATE;
OUTPUT OUT=SHIFTS N=NUMBER;

DATA SHIFTS;  /*GENERATES SETS OF SAMPLING SHIFTS WOUT REPLACEMENT*/
SET SHIFTS;
NUMBER=CEIL(NUMBER/2);
SET=1;
DO I=1 TO NUMBER;
  DO SHIFT=1 TO 2;
    %RAND;
    KEEP DAYTYPE SET UNIT SHIFT RANDOM;
    OUTPUT;
  END;
  SET=SET+1;
END;
PROC SORT;
BY DAYTYPE UNIT SET RANDOM;

DATA CALENDAR;  /*ADDS RANDOMLY ORDERED SHIFTS TO DATES*/
MERGE CALENDAR(IN=A) SHIFTS(IN=B);
BY DAYTYPE UNIT;
IF A AND B;
KEEP DATE DAYTYPE WEEK UNIT SHIFT STARTAM ENDAM STARTPM ENDPM;

DATA CALENDAR;  /*GENERATE INSTANTANEOUS COUNT TIMES*/
SET CALENDAR;
INTERVAL=(ENDAM-STARTAM)/&COUNTS;
%RAND;
TIME1=INT(RANDOM*INTERVAL);
IF SHIFT=1 THEN TIME1=TIME1+STARTAM;
IF SHIFT=2 THEN TIME1=TIME1+STARTPM;
%DO X=2 %TO &COUNTS;
  %LET Y=%EVAL(&X-1);
  TIME&X=TIME&Y + INTERVAL;
%END;
KEEP DATE DAYTYPE UNIT SHIFT TIME1-TIME&COUNTS STARTAM ENDAM
STARTPM ENDPM;
PROC SORT;
BY DAYTYPE UNIT SHIFT DATE;

PROC SUMMARY NWAY;     /*SELECT ORDER OF SITES TO BE SAMPLED*/
CLASSES DAYTYPE UNIT SHIFT;
VAR DATE;
OUTPUT OUT=NEW N=NUMBER;

DATA NEW; /*GENERATE RANDOM ORDER OF LAKES TO BE SAMPLED*/
SET NEW;
%DO X=1 %TO &UNITS;
  IF UNIT=&X THEN SNUM=SYMGET("SITES&X");
%END;
NUMBER=CEIL(NUMBER/SNUM);
SET=1;
DO I=1 TO NUMBER;
  DO SITE=1 TO SNUM;
    %RAND
    KEEP DAYTYPE UNIT SHIFT SITE SET RANDOM;
    OUTPUT;
  END;
  SET=SET+1;
END;
PROC SORT;
BY DAYTYPE UNIT SHIFT SET RANDOM;

DATA CALENDAR;
MERGE CALENDAR(IN=A) NEW(IN=B);
BY DAYTYPE UNIT SHIFT;
IF A AND B;
RETAIN PREV 0;
ARRAY TIMES (&COUNTS) TIME1-TIME&COUNTS;
IF FIRST.SHIFT THEN PREV=0;
KEEP DATE SITEX SHIFTX TIME1-TIME&COUNTS;
DO I=1 TO &COUNTS;
  IF STARTPM<TIMES(I)<ENDAM THEN DO;
    IF PREV=1 THEN TIMES(I)=.;
    IF PREV=1 THEN PREV=0; ELSE PREV=1;
  END;
END;
LENGTH TEMP1 TEMP2 SITEX $ 5 SHIFTX $ 20;
LENGTH SPACE $ 9;
IF SHIFT=1 THEN DO;
  TEMP1=PUT(STARTAM,HHMM5.);
  TEMP2=PUT(ENDAM,HHMM5.);
END;
ELSE DO;
  TEMP1=PUT(STARTPM,HHMM5.);
  TEMP2=PUT(ENDPM,HHMM5.);
END;
SPACE = '         ';
SHIFTX=SPACE||TEMP1||'-'||TEMP2;
SITEX=COMPRESS(UNIT||SITE);

PROC SORT;
BY DATE;

OPTIONS LINESIZE=200 PAGESIZE=80;
PROC CALENDAR DATA=CALENDAR HEADER=SMALL;
FORMAT TIME1-TIME&COUNTS HHMM5. SITEX $SITE.;
START DATE;
ID DATE;
VAR SITEX SHIFTX TIME1-TIME&COUNTS;
%MEND INTCOUNT;
